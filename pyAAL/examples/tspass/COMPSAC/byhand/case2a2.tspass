%% -*-Latex-*-
%%% 28/5/2015 
%%%%
%%%%=================================

%%%% CORRECTION 1 see case2a1.tspass
%%%% CORRECTION 2 :  prop 20 and 21
%%%% data from Kim or Sandra are not permitted to give from Hopital to Other
%%% and we also need to add the invariant related to this action (~give(hospital, Other, d) | Pgive(hospital, Other, d))

%%%%=================================

%%%%
~((
%%%%   IDENTITIES 
(always (kim(Kim) & sandra(Sandra) & other(Other))
            & (![a] ~(kim(a) <=> sandra(a)))
            & (![a] ~(kim(a) <=> other(a))))
%%% DATA KNOWLEDGE
& (always ![d] ((~(subjectKim(d) & subjectOther(d))) & (~(subjectSandra(d) & subjectOther(d)))))
%%% AUTHORISATIONS (deny incomplete)
%%%%%% P
& (always ![d]  (
    (subjectKim(d) => ~Presearch (hospital , d)) 
    & (subjectOther(d) => Presearch(hospital, d))
    & (subjectSandra(d) => Presearch(hospital, d))
    & (subjectKim(d) => Pgive(hospital, Kim, d))
    & ((~subjectKim(d)) => ~Pgive(hospital, Kim, d)) 
    & (subjectOther(d) => Pgive(hospital, Other, d))
%%%% 2: + deny give to other
    & (subjectKim(d) => ~Pgive(hospital, Other, d))
    & (subjectSandra(d) => ~Pgive(hospital, Other, d))
%%%% ---
    &  (subjectKim(d) => Psensor(Kim, cloudX, d))
    &  (subjectOther(d) => Psensor(Other, cloudX, d))
    &  (subjectKim(d) => Pcommunicate(Kim, Sandra, d))
    &  ((~subjectKim(d)) => ~Pcommunicate(Kim, Sandra, d))
    & ((subjectKim(d) | subjectOther(d)) => Ptransfer(cloudX , cloudZ, d))
    & ((subjectKim(d) | subjectOther(d)) => Pstore(cloudX , cloudY, d))
    & ((subjectKim(d) | subjectOther(d)) => Ptransfer2X(cloudZ , cloudX, d))
    & ((subjectKim(d) | subjectOther(d)) => Psend(cloudZ, hospital, d))
    & ((subjectKim(d) | subjectOther(d)) => Pput(cloudY, cloudX, d))
    & (subjectKim(d) => PaddIdent(Sandra, d))
    & ((subjectSandra(d) | subjectKim(d)) => Pupload(Sandra, cloudZ, d))
    & (subjectOther(d)  => ~Pupload(Sandra, cloudZ, d))
    & (subjectKim(d) => (Pread(Kim, d) & Pread(Sandra, d)))
    & (subjectKim(d) => (![a] ((~kim(a) & ~sandra(a)) => ~Pread(a, d))))
    & (subjectKim(d) => Pwrite(Kim, d))
    & (subjectKim(d) => (![a] ((~kim(a)) => (~Pwrite(a, d)))))
    & (subjectOther(d) =>  (Pread(Other, d) & Pwrite(Other, d)))
    & (subjectOther(d) => (![a] ((~other(a)) => (~Pwrite(a, d) & ~Pread(a,d)))))
))
%%%% INVARIANT is needed 
& (always ![d] (
(![a] (~write(a, d) | Pwrite(a, d)))
& (![a] (~read(a, d) | Pread(a, d)))
& (~sensor(Kim,cloudX, d) | Psensor(Kim,cloudX, d))
& (~research(hospital, d) | Presearch(hospital, d))
& (~sensor(Kim,cloudX, d) | Psensor(Kim,cloudX, d))
& (~store(cloudX , cloudY, d) | Pstore(cloudX , cloudY, d))
& (~communicate(Kim, Sandra, d) | Pcommunicate(Kim, Sandra, d))
& (~addIdent(Sandra, d) | PaddIdent(Sandra, d))
& (~upload(Sandra, cloudZ, d) | Pupload(Sandra, cloudZ, d))
& (~transfer(cloudX , cloudZ, d) | Ptransfer(cloudX , cloudZ, d))
& (~send(cloudZ, hospital, d) | Psend(cloudZ, hospital, d))
& (~give(hospital, Kim, d) | Pgive(hospital, Kim, d))
& (~transfer2X(cloudZ, cloudX, d) | Ptransfer2X(cloudZ, cloudX, d))
& (~put(cloudY, cloudX, d) | Pput(cloudY, cloudX, d))
%%% 2: add give to Other
& (~give(hospital, Other, d) | Pgive(hospital, Other, d))
))
%% %% %%%% BEHAVIOR unconstrained
& (always ![d] (sensor(Kim, cloudX, d) 
    %%% should constraint the communication
          & (subjectKim(d) => communicate(Kim, Sandra, d) )
          & upload(Sandra, cloudZ, d)
    &  (![a] (sensor(a,cloudX, d) => sometime transfer(cloudX , cloudZ, d)))
    &  (![a] (sensor(a,cloudX, d) => sometime store(cloudX , cloudY, d)))
    &  (store(cloudX , cloudY, d) => sometime put(cloudY, cloudX, d))
    &  (put(cloudY, cloudX, d) => sometime sensor(Kim,cloudX, d))
    &  (put(cloudY, cloudX, d) => sometime transfer(cloudX , cloudZ, d))
    &  (communicate(Kim, Sandra, d) => sometime upload(Sandra, cloudZ, d))
    &  (communicate(Kim, Sandra, d) => sometime addIdent(Sandra, d))
    &  (upload(Sandra, cloudZ, d) => sometime send(cloudZ, hospital, d))
    &  (upload(Sandra, cloudZ, d) => sometime transfer2X(cloudZ, cloudX, d))
    &  (transfer(cloudX , cloudZ, d)  => sometime send(cloudZ, hospital, d))
    &  (transfer(cloudX , cloudZ, d)  => sometime transfer2X(cloudZ, cloudX, d))
    &  (transfer2X(cloudZ, cloudX, d) => sometime transfer(cloudX , cloudZ, d))
    &  (transfer2X(cloudZ, cloudX, d) => sometime send(cloudZ, hospital, d))
    &  (send(cloudZ, hospital, d)  => sometime research(hospital, d))
    &  (send(cloudZ, hospital, d) => sometime give(hospital, Kim, d))
    &  (send(cloudZ, hospital, d) => sometime give(hospital, Other, d))
    &  (![a] (give(hospital, a, d) => sometime sensor(Kim,cloudX, d)))
    &  (![a] (give(hospital, a, d) => sometime sensor(Other,cloudX, d)))
    &  (give(hospital, Kim, d) => sometime communicate(Kim, Sandra, d))
))
) %%% END COND is SAT
=>
%%% ------- PROPERTIES 

%%% Note (time user / nb clause generated)
%%%%%%  ---------  hospital: any data from Kim or from Sandra  can be received via send to hospital [1 - 2]
%%%(![d] (sometime ((subjectKim(d) | subjectSandra(d)) => send(cloudZ, hospital, d)))) %%% VALID (0.045/458)
%%% (![d] (sometime ((subjectKim(d) | subjectSandra(d) | subjectOther(d)) => send(cloudZ, hospital, d)))) %%% VALID (0.045/440)

%%%%% --------- Kim receives Kim's data [3 - 8]
%%%(sometime ?[d] (subjectKim(d) => (sometime give(hospital, Kim, d)))) %%% VALID (0.045/459)
%%% (?[d] sometime (subjectKim(d) => (sometime give(hospital, Kim, d)))) %%% VALID (0.051/645)
%%% (![d] (sometime (subjectKim(d) => give(hospital, Kim, d)))) %%%VALID (0.047/527)
%%%% ---------  Kim receives shared data with Sandra
%%% (![d] (sometime ((subjectKim(d) & subjectSandra(d)) => give(hospital, Kim, d)))) %%%VALID (0.048/538)
%%%% ---------  Kim cannot receive Sandra's proper data
%%% (![d] (always ~(subjectSandra(d) & ~subjectKim(d) & give(hospital, Kim, d)))) %%% VALID (0.054/773)
%%% another formulation
%%% ~(?[d] (sometime (((~subjectKim(d)) & subjectSandra(d)) & give(hospital, Kim, d)))) %%% VALID  (0.054/773)

%%%%%% ---------  What Sandra can receive ? [9 - 14]
%%%% data from Kim quite obvious via communicate
%%% (![d] (always (subjectKim(d) => communicate(Kim, Sandra, d)))) %%%VALID (0.047/480)
%%% and shared data
%%% (![d] (always ((subjectKim(d) & subjectSandra(d)) => communicate(Kim, Sandra, d)))) %%%VALID (0.048/503)
%%% data from Sandra and not from Kim ?
%%% (?[d] (sometime (~subjectKim(d) & subjectSandra(d) & communicate(Kim, Sandra, d)))) %%% sat ONLY
%%%% CORRECTION 1
%%% (![d] always ((~subjectKim(d) & subjectSandra(d)) => ~communicate(Kim, Sandra, d))) %%% VALID (0.055/686)
%%% ~(?[d] (sometime (~subjectKim(d) & subjectSandra(d) & communicate(Kim, Sandra, d)))) %%% VALID (0.053/686)
%%% (![d] (always ~(~subjectKim(d) & subjectSandra(d) & communicate(Kim, Sandra, d)))) %%% VALID (0.054/686)
%%% (![d] (always (subjectKim(d) | ~subjectSandra(d) | ~communicate(Kim, Sandra, d)))) %%% VALID (0.050/686)

%%%%%% ---------  What kinds of data is reaching cloudX ? (incomplete ...) [15 - 17]
%%%(![d] (sometime ((subjectKim(d) | subjectSandra(d)) => (sensor(Kim, cloudX, d) | transfer2X(cloudZ, cloudX, d) | put(cloudY, cloudX, d))))) %%% VALID (0.043/378)
%%% (![d] (always ((subjectKim(d) | subjectSandra(d)) => (sensor(Kim, cloudX, d) | transfer2X(cloudZ, cloudX, d) | put(cloudY, cloudX, d))))) %%% VALID (0.048/402)
%%% (![d] (always ((subjectKim(d) | subjectSandra(d) | subjectOther(d)) => (sensor(Kim, cloudX, d) | transfer2X(cloudZ, cloudX, d) | put(cloudY, cloudX, d) | sensor(Other, cloudX, d))))) %%% VALID (0.046/403)

%%%%% --------- Other receive Kim's data or Sandra's data ? [18 - 22]
%%%(sometime ?[d]  (subjectKim(d) => ~give(hospital, Other, d))) %%% VALID (0.043/398)
%%% (![d] sometime (subjectKim(d) => ~give(hospital, Other, d))) %%% VALID (0.048/508)
%%%% CORRECTION 2 
%%% (![d] sometime ((subjectKim(d) | subjectSandra(d)) => ~give(hospital, Other, d))) %%% -> VALID (0.047/501)
%%% (![d] always ~((subjectKim(d) | subjectSandra(d)) & give(hospital, Other, d))) %%% sat -> VALID (0.057/786)

%%%%% -------- hospital cannot do research on Kim data [23 - 25]
(![d] always ((subjectKim(d) & send(cloudZ, hospital, d)) => ~research(hospital, d))) %%% VALID (0.054/706)
%%% (![d] always (~(subjectKim(d)  & research(hospital, d)))) %%% VALID (0.053/678)
%%% (![d] always (subjectKim(d) => ~research(hospital, d))) %%% VALID (0.052/678)

%%%%% --------- hospital can do research on Sandra or Other data [26 - 27]
%%% (?[d] (sometime ((subjectSandra(d) | subjectOther(d)) => research(hospital, d)))) %%% VALID (0.048/530)
%%% (?[d]  ((subjectSandra(d) | subjectOther(d)) => sometime research(hospital, d))) %%% VALID (0.047/508)

%%%%% --------- hospital can receive data from the three agents [28 - 32]
%%% (?[d] sometime  (subjectOther(d) => (send(cloudZ, hospital, d) & Psend(cloudZ, hospital, d)))) %%% VALID (0.048/511)
%%% (?[d] sometime  (subjectKim(d) => (send(cloudZ, hospital, d) & Psend(cloudZ, hospital, d)))) %%% VALID (0.047/514)
%%% (?[d] sometime  (subjectSandra(d) => (send(cloudZ, hospital, d) & Psend(cloudZ, hospital, d)))) %%% VALID (0.047/501)
%%% (?[d] sometime  ((subjectOther(d) | subjectKim(d) | subjectSandra(d)) => (send(cloudZ, hospital, d) & Psend(cloudZ, hospital, d)))) %%% VALID (0.046/498)
%%%(?[d] sometime  ((subjectOther(d) | subjectKim(d) | subjectSandra(d)) => send(cloudZ, hospital, d))) %%% VALID (0.046/440)

) %%% end of imply

