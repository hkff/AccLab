/**
 * AccLab UserGuide : Getting started
 * Tuto0
 * Author : Walid Benghabrit
 */

// Loading types & macros libraries
LOAD "core.types"
LOAD "core.macros"

// Declaring services
SERVICE read
SERVICE store
SERVICE delete

// Declaring actors
AGENT alice TYPES(Actor) REQUIRED(read store delete) PROVIDED()
AGENT bob   TYPES(Actor) REQUIRED(read store delete) PROVIDED()
AGENT css   TYPES(Actor) REQUIRED() PROVIDED(read store delete)

/*
 * Cloud storage service provider policy
 */
CLAUSE css_policy (
    FORALL d:data FORALL a:Actor

    // Allow users to read their data
    IF (d.subject == a) THEN {
        PERMIT a.read[css](d)
    } AND

    // Deny access to read other
    IF (d.subject != a) THEN {
        DENY a.read[css](d) BEFORE "1 year"
    } AND

    // Allow css to read/delete stored data
    PERMIT css.read[css](d) BEFORE "2 Days" AND
    PERMIT css.delete[css](d) BEFORE "5 Months"
)

/*
 * Alice's preferences
 */
CLAUSE alice_pref (
    FORALL d:data
    // Alice want to be able to read all her data stored on css
    IF (d.subject == alice) THEN {
        PERMIT alice.read[css](d) BEFORE "6 Months"
    }
)

// Call validate macro to perform the compliance check
CALL validate("css_policy" "alice_pref")
