\section{AAL language}
\begin{lstlisting}[caption={AAL Syntax}, label=syntax]
    // AAL CORE
    AALprogram    ::= (Declaration | Clause | Comment | Macro | MacroCall | Loadlib
                    | LtlCheck | CheckApply | Exec | Behavior)
    Declaration   ::= AgentDec | ServiceDec | DataDec | TypesDec | varDec
    AgentDec      ::= AGENT Id [TYPES '(' Type *')' REQUIRED'('service*')' PROVIDED'('service*')']
    ServiceDec    ::= SERVICE Id [TYPES '(' Type* ')'] [PURPOSE '(' Id* ')']
    DataDec       ::= DATA Id TYPES '(' Type* ')' [REQUIRED'('service*')' PROVIDED'('service*')'] SUBJECT agent
    VarDec        ::= Type_Id Id [attr_Id '(' value* ')']*
    Clause        ::= CLAUSE Id '(' [Usage] [Audit Rectification] ')'
    Usage         ::= ActionExp
    Audit         ::= AUDITING Usage
    Rectification ::= IF_VIOLATED_THEN Usage
    ActionExp     ::= Action | NOT ActionExp | Modality ActionExp | Condition
                    | ActionExp (AND|OR|ONLYWHEN|UNTIL|UNLESS) ActionExp
                    | Author | Quant* | IF '(' ActionExp ')' THEN '{' ActionExp '}'
    Exp           ::= Variable | Constant | Variable.Attribute
    Condition     ::= [NOT] Exp | Exp ['==' | '!='] Exp | Condition (AND|OR) Condition
    Author        ::= (PERMIT | DENY) Action
    Action        ::= agent.service ['['[agent]']'] '('Exp')' [Time] [Purpose]
    Quant         ::= (FORALL | EXISTS) Var [WHERE Condition]
    Variable      ::= Var ':' Type
    Modality      ::= MUST | MUSTNOT | ALWAYS | NEVER | SOMETIME
    Time          ::= (AFTER | BEFORE) Date | Time (AND | OR) Time
    Date          ::= STRING
    Type, var, val, attr Id, agent, Constant, Purpose ::= literal

    // AAL Type extension
    TypesDec      ::= TYPE Id [EXTENDS '(' Type* ')'] ATTRIBUTES '(' AttributeDec* ')' ACTIONS '(' ActionDec* ')'
    AttributeDec  ::= Id ':' Type
    ActionDec     ::= Id
    Type, Id      ::= litteral

    // Reflexion extension
    Macro         ::= MACRO Id '(' param* ')' '(' mcode ')'
    MCode         ::= """ Meta model api + Python3 code (subset) """
    MCall         ::= CALL Id '(' param* ')'
    LoadLib       ::= LOAD STRING;
    Exec          ::= EXEC MCode

    // FOTL checking extension
    LtlCheck     ::= CHECK Id '(' param* ')' '(' check ')'
    check        ::= FOTL_formula + clause(Id) [.ue | .ae | .re]
    CheckApply   ::= APPLY Id '(' param* ')'

    // Behavior extension
    Behavior    ::= BEHAVIOR Id '(' ActionExp ')'

\end{lstlisting}